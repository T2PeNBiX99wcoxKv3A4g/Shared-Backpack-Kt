import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.fabric.loom)
    `maven-publish`
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.translation.generator)
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    maven("https://maven.fzzyhmstrs.me/") {
        name = "FzzyMaven"
    }
    maven("https://maven.terraformersmc.com/") {
        name = "Terraformers"
    }
    maven("https://maven.parchmentmc.org") {
        name = "Parchment"
    }
    exclusiveContent {
        forRepository {
            maven("https://api.modrinth.com/maven") {
                name = "Modrinth"
            }
        }
        filter {
            includeGroup("maven.modrinth")
        }
    }
}

loom {
    mods {
        register(providers.gradleProperty("mod_id").get()) {
            sourceSet(sourceSets.main.get())
        }
    }

    val aw = file("src/main/resources/${providers.gradleProperty("mod_id").get()}.aw")
    if (aw.exists())
        accessWidenerPath = aw
}

dependencies {
    // To change the versions see the libs.versions.toml file
    minecraft(libs.minecraft)
    mappings(
        loom.layered {
            officialMojangMappings()
            parchment(
                "org.parchmentmc.data:parchment-${
                    libs.versions.minecraft.get()
                }:${libs.versions.parchment.mappings.get()}@zip"
            )
        }
    )
    modImplementation(libs.fabric.loader)

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation(libs.fabric.api)
    modImplementation(libs.fabric.language.kotlin)
    modImplementation(libs.modmenu)
    modImplementation(libs.fzzy.config)
}

tasks.processResources {
    val version = version
    inputs.property("version", version)
    inputs.property("minecraft_version", libs.versions.minecraft.get())
    inputs.property("loader_version", libs.versions.fabric.loader.get())
    filteringCharset = "UTF-8"

    filesMatching("fabric.mod.json") {
        expand(
            "version" to version,
            "mod_id" to providers.gradleProperty("mod_id").get(),
            "mod_name" to providers.gradleProperty("mod_name").get(),
            "minecraft_version" to libs.versions.minecraft.get(),
            "loader_version" to libs.versions.fabric.loader.get(),
            "fabric_kotlin_version" to libs.versions.fabric.kotlin.get(),
            "fabric_api_version" to libs.versions.fabric.api.get(),
            "mod_menu_version" to libs.versions.modmenu.get(),
            "fzzy_config_version" to libs.versions.fzzy.config.get(),
        )
    }

    dependsOn("generateUpsideDownTranslation")
    from(
        layout.buildDirectory.dir(
            "generated/resources/upsideDownTranslations"
        )
    )
}

tasks.withType<JavaCompile>().configureEach {
    options.release = libs.versions.jvm.target.get().toInt()
}

kotlin {
    jvmToolchain(libs.versions.jvm.toolchain.get().toInt())
    compilerOptions {
        jvmTarget = JvmTarget.fromTarget(libs.versions.jvm.target.get())
    }

    sourceSets {
        main {
            kotlin.srcDir(
                layout.buildDirectory.dir(
                    "generated/sources/fallbackTranslations"
                )
            )
        }
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

tasks.jar {
    val projectName = project.name
    inputs.property("projectName", projectName)

    from("LICENSE") {
        rename { "${it}_$projectName" }
    }
}

// configure the maven publication
publishing {
    publications {
        register<MavenPublication>("mavenJava") {
            from(components["java"])
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

tasks.named<KotlinCompile>("compileKotlin") {
    dependsOn("generateFallbackTranslations")
}

tasks.named<Jar>("sourcesJar") {
    dependsOn("generateFallbackTranslations")
}

loom {
    runs {
        configureEach {
            vmArg("-Dsodium.checks.issue2561=false")
        }
    }
}

translationGenerator {
    modId = providers.gradleProperty("mod_id")
    packageName = providers.gradleProperty("package_group")
}